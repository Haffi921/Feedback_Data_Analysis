Const PROGRAM_NAME = "ZBin"

' New Node
Dim OperationInfo As String
Dim nhn As New NewHistoryNode

' Operation Info / Input variables
Dim Selected_Channels() As String
Dim Startpoint As Long
Dim Interval_Length As Long
Dim ZBin_Start As Long
Dim ZBin_End As Long
Dim Include_Parent As Boolean

' Data
Dim Data_Length As Long
Dim Segment_Length As Long

' Channels
Dim Nr_Channels As Long
' Segments
Dim Nr_Segments As Long
' Intervals
Dim Nr_Intervals As Long


' Z calculation
Dim WorkData() As Single
Dim MeanList() As Single
Dim MeanDiffList() As Single
Dim Bad_Intervals() As Boolean

' FastArray object for fast calculation
Dim DataArray As New FastArray

' -------------------------------
' Main
' -------------------------------
Sub Main
	Debug.Clear

	' -------------------------------
	' Node Check
	' -------------------------------
	CheckActiveNode
	' -------------------------------

	' -------------------------------
	' Operation Info
	' -------------------------------
	GenerateOperationInfo
	' -------------------------------

	' -------------------------------
	' Initialization
	' -------------------------------
	InitializeConstants
	InitializeArrays
	' -------------------------------

	' -------------------------------
	' RMS Processing
	' -------------------------------
	GetData
	ProcessData
	' -------------------------------

	' -------------------------------
	' Find outliers
	' -------------------------------
	Find_Bad_Intervals(MeanList)
	Find_Bad_Intervals(MeanDiffList)
	'Print_Outliers
	' -------------------------------

	' -------------------------------
	' Node creation
	' -------------------------------
	CreateNewNode(PROGRAM_NAME)
	' -------------------------------
End Sub

Sub Print_Outliers
	Dim Count As Integer

	For Channel_Nr = LBound(Bad_Intervals, 1) To UBound(Bad_Intervals, 1)
		Count = 0
		For Segment_Nr = LBound(Bad_Intervals, 2) To UBound(Bad_Intervals, 2)
			For Interval_Nr = LBound(Bad_Intervals, 3) To UBound(Bad_Intervals, 3)
				If Bad_Intervals(Channel_Nr, Segment_Nr, Interval_Nr) Then
					Count = Count + 1
					'Debug.Print Channel_Nr & "-" & Segment_Nr & "-" & Interval_Nr & ": " & MeanList(Channel_Nr, Segment_Nr, Interval_Nr)
				End If
			Next Interval_Nr
		Next Segment_Nr
		Debug.Print Count
	Next Channel_Nr
End Sub


Sub InitializeConstants
	' Whole data length
	Data_Length = ActiveNode.Dataset.Length

	' Channels
	Nr_Channels = ActiveNode.Dataset.Channels.Count

	' Segments
	Nr_Segments = ActiveNode.Segments.Count
	Segment_Length = Data_Length / Nr_Segments

	CheckAllSegmentsAreEqual

	' Intervals
	Nr_Intervals = Data_Length / Nr_Segments / Interval_Length

	CheckZBinSelection
End Sub

Sub InitializeArrays
	' Arrays
	'ReDim WorkData(ActiveNode.Dataset.Length
	'' Z Calculation ''
	ReDim MeanList(Nr_Parent_Channels + 1 To Nr_Channels, 1 To Nr_Segments, ZBin_Start To ZBin_End)
	ReDim MeanDiffList(Nr_Parent_Channels + 1 To Nr_Channels, 1 To Nr_Segments, ZBin_Start + 1 To ZBin_End)
	ReDim Bad_Intervals(Nr_Parent_Channels + 1 To Nr_Channels, 1 To Nr_Segments, ZBin_Start To ZBin_End)
End Sub
' -------------------------------

' -------------------------------
' Checks
' -------------------------------
Sub CheckActiveNode
  	If Not ActiveNode.DataAvailable Then
		MsgBox "This macro needs an open data window."
		Exit All
	End If
	If ActiveNode.Dataset.Type <> viDtTimeDomain Then
		MsgBox "Current Dataset is of type HEX" & Hex(ActiveNode.Dataset.Type) &  ". This macro expects data in the Time domain = HEX 101."
		Exit All
	End If
End Sub

Sub CheckAllSegmentsAreEqual
	For seg = 1 To Nr_Segments
		If ActiveNode.Segments(seg).Dataset.Length <> Segment_Length Then
			MsgBox "Error: Not all segments are of the same length. This is required!"
			Exit All
		End If
	Next seg
End Sub

Sub CheckZBinSelection
	If ZBin_End > Nr_Intervals Then
		MsgBox "Incorrect bin range as input. ZBin end cannot be more than number of bins!" & vbCrLf & "Amount of Bins: " & Nr_Intervals
		Exit All
	End If
End Sub


Sub CheckOperationInfo
	If (Startpoint <= 0) Or (Interval_Length <= 0) Then
		MsgBox "Incorrect datapoint ranges as input. Macro execution aborted."
		Exit All
	End If

	If ZBin_Start <= 0 Then
		MsgBox "Incorrect bin range as input. ZBin start must be a positive integer!"
		Exit All
	End If

	If ZBin_End <= 0 Then
		MsgBox "Incorrect bin range as input. ZBin end must be a positive integer!"
		Exit All
	End If

	If ZBin_Start >= ZBin_End Then
		MsgBox "Incorrect bin range as input. ZBin start must be less than ZBin end!"
		Exit All
	End If
End Sub
' -------------------------------

' -------------------------------
' Operation Info
' -------------------------------
Sub GenerateOperationInfo
	If Application.TemplateMode Then
		OperationInfo = ActiveTemplateNode.Description
		GetOperationInfo
	Else
		ShowMainDialog
		OperationInfo = CreateOperationInfo()
	End If
	CheckOperationInfo
End Sub

Sub ShowMainDialog
	Dim Channel_Names(ActiveNode.Dataset.Channels.Count) As String

	' Get Channel names in ActiveNode
	For Channel_Nr = 1 To ActiveNode.Dataset.Channels.Count
		Channel_Names(Channel_Nr) = ActiveNode.Dataset.Channels(Channel_Nr).Name
	Next Channel_Nr

	Begin Dialog UserDialog 400, 282
		' Channel options
		MultiListBox 20, 10, 360, 100, Channel_Names, .Selected_Channels

		' Bin options
		Text 20, 122, 70, 12, "Startpoint:"
		TextBox 130, 120, 90, 18, .Startpoint$
		Text 20, 142, 110, 12, "Interval length:"
		TextBox 130, 140, 90, 18, .Interval_Length$

		' Z calculation options
		Text 20, 168, 350, 12, "Select bin range to include for Z-score calculation:"
		Text 40, 187, 70, 12, "Start:"
		TextBox 90, 185, 90, 18, .ZBin_Start$
		Text 43, 205, 70, 12, "End:"
		TextBox 90, 203, 90, 18, .ZBin_End$

		' Buttons
		OKButton 110, 250, 40, 20
		CancelButton 230, 250, 60, 20
	End Dialog
	Dim dlg As UserDialog

	' Default Values
	dlg.Startpoint$ = "1"
	dlg.Interval_Length$ = "50"
	dlg.ZBin_Start$ = "1"
	dlg.ZBin_End$ = "20"

	' User presses cancel
	If Dialog(dlg) = 0 Then
		Exit All
	End If

	' User doesn't select any channels
	If UBound(dlg.Selected_Channels) = -1 Then
		MsgBox("No channels selected")
		Exit All
	End If

	' Get Values
	ReDim Selected_Channels(1 To UBound(dlg.Selected_Channels) + 1)

	For Channel_Nr = 0 To UBound(dlg.Selected_Channels)
		Selected_Channels(Channel_Nr + 1) = Channel_Names(dlg.Selected_Channels(Channel_Nr) + 1)
	Next Channel_Nr

	Startpoint = CInt(dlg.Startpoint$)
	Interval_Length = CInt(dlg.Interval_Length$)
	ZBin_Start = CInt(dlg.ZBin_Start$)
	ZBin_End = CInt(dlg.ZBin_End$)
End Sub

Sub GetOperationInfo
	Selected_Channels = Split(FindInStrBetween(OperationInfo, "Channels: ", vbCrLf), ";")

	Startpoint = CInt(FindInStrBetween(OperationInfo, "Startpoint: ", vbCrLf))
	Interval_Length = CInt(FindInStrBetween(OperationInfo, "Interval: ", "points"))

	ZBin_Start = CInt(FindInStrBetween(OperationInfo, "Bin Range: ", "-"))
	ZBin_End = CInt(FindInStrBetween(OperationInfo, "Bin Range: " & CStr(ZBin_Start) & "-", " (used"))
End Sub

Function CreateOperationInfo As String
    Dim nhnString As String

    nhnString = "*** " & PROGRAM_NAME & " written by Henk van Steenbergen, Leiden University ***" & vbCrLf
    nhnString = nhnString & "*** Revised and rewritten by Hafsteinn Ragnarsson, Tübingen University ***" & vbCrLf
    nhnString = nhnString & "--------------------------------------------------------------------------------------------" & vbCrLf

	nhnString = nhnString & vbCrLf
	nhnString = nhnString & "Channels: " & Join(Selected_Channels, ";") & vbCrLf
	nhnString = nhnString & "Startpoint: " & CStr(Startpoint) & vbCrLf
	nhnString = nhnString & "Interval: " & CStr(Interval_Length) & " points" & vbCrLf
	nhnString = nhnString & "Bin Range: " & CStr(ZBin_Start) & "-" & CStr(ZBin_End) & " (used for z-score calc.)" & vbCrLf

    nhnString = nhnString & vbCrLf
    nhnString = nhnString & "--------------------------------------------------------------------------------------------" & vbCrLf

    CreateOperationInfo = nhnString
End Function
' -------------------------------

' -------------------------------
' Get Input Data
' -------------------------------
Sub GetData
	WorkData = ActiveNode.Dataset.GetData(1, ActiveNode.Dataset.Length)
End Sub
' -------------------------------

' -------------------------------
' Main RMS Process
' -------------------------------
Sub ProcessData
	Dim Channel_Nr As Long
	Dim Segment_Nr As Long
	Dim Interval_Nr As Long

	Dim Datapoint As Long
	Dim Avg As Single

	Dim pb As New ProgressBar
	pb.Init("Creating bins...", "Please wait...")
	pb.SetRange(1, Nr_Extra_Channels * Nr_Segments * (ZBin_End - ZBin_Start))
	pb.SetStep(1)

	' Loop through all added channels
	For Channel_Nr = 1 To Nr_Channels

		' Loop through all segments
		For Segment_Nr = 1 To Nr_Segments

			' Loop through all intervals of segment
			For Interval_Nr = ZBin_Start To ZBin_End
				If pb.UserCanceled Then
					Exit All
				End If

				Datapoint = GetMultipointAt(Channel_Nr, Segment_Nr, Interval_Nr)

				' Calculate Avg
				Avg = DataArray.GetMeanValue(WorkData, Datapoint, Nr_Channels, Interval_Length)

				' Collect Avg for Z calculation
				If Interval_Nr >= ZBin_Start And Interval_Nr <= ZBin_End Then
					MeanList(Channel_Nr, Segment_Nr, Interval_Nr) = Avg

					If Interval_Nr <> ZBin_Start Then
						MeanDiffList(Channel_Nr, Segment_Nr, Interval_Nr) = Avg - MeanList(Channel_Nr, Segment_Nr, Interval_Nr - 1)
					End If
				End If

				pb.StepIt()
			Next Interval_Nr

		Next Segment_Nr

	Next Channel_Nr
End Sub
' -------------------------------

' -------------------------------
' Interval marking
' -------------------------------
Sub MarkIntervals
	Dim Channel_Nr As Long
	Dim Segment_Nr As Long
	Dim Interval_Nr As Long

	Dim pb As New ProgressBar
	pb.Init("Marking bad intervals...", "Please wait...")
	pb.SetRange(1, Nr_Extra_Channels * Nr_Segments * (ZBin_End - ZBin_Start))
	pb.SetStep(1)

	For Channel_Nr = LBound(Bad_Intervals, 1) To UBound(Bad_Intervals, 1)
		For Segment_Nr = LBound(Bad_Intervals, 2) To UBound(Bad_Intervals, 2)
			For Interval_Nr = LBound(Bad_Intervals, 3) To UBound(Bad_Intervals, 3)
				If pb.UserCanceled Then
					Exit All
				End If

				If Bad_Intervals(Channel_Nr, Segment_Nr, Interval_Nr) Then
					nhn.AddMarker(Channel_Nr, GetDatapointAt(Segment_Nr, Interval_Nr), Interval_Length, "BI", "Bad Interval")
				End If

				pb.StepIt()
			Next Interval_Nr
		Next Segment_Nr
	Next Channel_Nr
End Sub

Function Find_Bad_Intervals(list() As Single)
	Dim Channel_Nr As Long
	Dim Segment_Nr As Long
	Dim Interval_Nr As Long

	ReDim InterSeg(LBound(list, 3) To UBound(list, 3)) As Single
	ReDim CrossSeg(LBound(list, 2) To UBound(list, 2)) As Single

	Dim Mean_Avg As Single
	Dim Std_Avg As Single

	Dim pb As New ProgressBar
	pb.Init("Finding bad intervals...", "Please wait...")
	pb.SetRange(1, Nr_Extra_Channels * Nr_Segments * (UBound(list, 3) - LBound(list, 3)) * 2)
	pb.SetStep(1)

	For Channel_Nr = LBound(list, 1) To UBound(list, 1)
		' InterSegment
		For Segment_Nr = LBound(list, 2) To UBound(list, 2)
			For Interval_Nr = LBound(list, 3) To UBound(list, 3)
				If pb.UserCanceled Then
					Exit All
				End If
				InterSeg(Interval_Nr) = list(Channel_Nr, Segment_Nr, Interval_Nr)
			Next Interval_Nr

			Mean_Avg = DataArray.GetMeanValue(InterSeg)
			Std_Avg = Std(InterSeg, Mean_Avg, UBound(InterSeg) - LBound(InterSeg) + 1)

			For Interval_Nr = LBound(list, 3) To UBound(list, 3)
				If pb.UserCanceled Then
					Exit All
				End If

				z = Abs((InterSeg(Interval_Nr) - Mean_Avg) / Std_Avg)

				If z > 3.5 Then
					Bad_Intervals(Channel_Nr, Segment_Nr, Interval_Nr) = True
					'Debug.Print Channel_Nr & "-" & Segment_Nr & "-" & Interval_Nr & ": " & list(Channel_Nr, Segment_Nr, Interval_Nr) & " -> " & z
				End If

				pb.StepIt()
			Next Interval_Nr

		Next Segment_Nr

		' CrossSegment
		For Interval_Nr = LBound(list, 3) To UBound(list, 3)
			For Segment_Nr = LBound(list, 2) To UBound(list, 2)
				If pb.UserCanceled Then
					Exit All
				End If
				CrossSeg(Segment_Nr) = list(Channel_Nr, Segment_Nr, Interval_Nr)
			Next Segment_Nr

			Mean_Avg = DataArray.GetMeanValue(CrossSeg)
			Std_Avg = Std(CrossSeg, Mean_Avg, UBound(CrossSeg) - LBound(CrossSeg) + 1)

			For Segment_Nr = LBound(list, 2) To UBound(list, 2)
				If pb.UserCanceled Then
					Exit All
				End If

				z = Abs((CrossSeg(Segment_Nr) - Mean_Avg) / Std_Avg)

				If z > 3.5 Then
					Bad_Intervals(Channel_Nr, Segment_Nr, Interval_Nr) = True
					'Debug.Print Channel_Nr & "-" & Segment_Nr & "-" & Interval_Nr & ": " & list(Channel_Nr, Segment_Nr, Interval_Nr) & " -> " & z
				End If

				pb.StepIt()
			Next Segment_Nr
		Next Interval_Nr
	Next Channel_Nr
End Function
' -------------------------------

' -------------------------------
' Final Node Creation
' -------------------------------
Function CreateNewNode(Node_Name As String)
	nhn.Create(Node_Name, ActiveNode)
	MarkIntervals
	nhn.Description = OperationInfo
	nhn.Finish
End Function
' -------------------------------

' -------------------------------
' Helper functions
' -------------------------------
Function GetDatapointsUpTo(Segment_Nr As Long, Interval_Nr As Long) As Long
	GetDatapointsUpTo = ((Segment_Nr - 1) * Segment_Length) + ((Interval_Nr - 1) * Interval_Length) + (Startpoint - 1)
End Function

Function GetDatapointAt(Segment_Nr As Long, Interval_Nr As Long) As Long
	GetDatapointAt = GetDatapointsUpTo(Segment_Nr, Interval_Nr) + 1
End Function

Function GetMultipointAt(Channel_Nr As Long, Segment_Nr As Long, Interval_Nr As Long) As Long
	GetMultipointAt = (GetDatapointsUpTo(Segment_Nr, Interval_Nr) * Nr_Channels) + Channel_Nr
End Function

Function FindInStrBetween(SearchString As String, StartString As String, EndString As String) As String
	Dim pos, Length As Integer
	pos = InStr(SearchString, StartString) + Len(StartString)
	Length = InStr(pos, SearchString, EndString) - pos
	FindInStrBetween = Mid(SearchString, pos, Length)
End Function

Function Std(list() As Single, Mean As Single, N As Integer)
	Std = Sum_Of_Square(list, Mean)
	If N > 2 Then
		Std = Std / (N - 1)
	End If
	Std = Sqr(Std)
End Function

Function Sum(list() As Single) As Single
	For i = LBound(list) To UBound(list)
		Sum = Sum + list(i)
	Next i
End Function

Function Sum_Of_Square(list() As Single, Mean As Single) As Single
	Dim tmp() As Single
	ReDim tmp(LBound(list) To UBound(list))
	DataArray.CopyArray(tmp, list)
	DataArray.SubtractValue(tmp, Mean)
	DataArray.SquareArray(tmp)
	Sum_Of_Square = Sum(tmp)
End Function
' -------------------------------
